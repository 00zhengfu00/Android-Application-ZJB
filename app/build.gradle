apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getDefault())
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.idrv.coach"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 400
        versionName "4.0.0"
    }
    signingConfigs {
        debug {
            storeFile file("ilejia.jks")
            storePassword "lja123456"
            keyAlias "ilejia@163.com"
            keyPassword "lja123456"
        }
        release {
            storeFile file("ilejia.jks")
            storePassword "lja123456"
            keyAlias "ilejia@163.com"
            keyPassword "lja123456"
        }
    }

    buildTypes {
        def BOOLEAN = "boolean"
        def TRUE = "true"
        def FALSE = "false"
        def DEBUG_ENABLE = "DEBUG_ENABLE"

        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            debuggable true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField BOOLEAN, DEBUG_ENABLE, TRUE
            manifestPlaceholders = [PUSH_APPID    : "AEG1H6OkLJ9tAsX2PR3A16",
                                    PUSH_APPKEY   : "OsWfyXsjij6IzTQkwq3is4",
                                    PUSH_APPSECRET: "R9Z6LIo5IU6fkM3YLL3mG1",
                                    PUSH_GROUPID  : "",
                                    TD_APPKEY     : "AA1B049E3E0E28B285A98F4442BCA2AE"]
        }
        release {
            zipAlignEnabled true
            debuggable false
            shrinkResources true
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField BOOLEAN, DEBUG_ENABLE, FALSE
            manifestPlaceholders = [PUSH_APPID    : "HCl3iq4tW4960srRBNKCz3",
                                    PUSH_APPKEY   : "qFKTzwfPFz92UOE5G9r4j1",
                                    PUSH_APPSECRET: "wIXzgjV9on8rHWQS075995",
                                    PUSH_GROUPID  : "",
                                    TD_APPKEY     : "AA1B049E3E0E28B285A98F4442BCA2AE"]
        }
    }

    productFlavors {
        alpha {}
        //360渠道
        _360 {}
        //小米渠道
        xiaomi {}
        //豌豆荚
        wandoujia {}
        oppo {}
        //应用宝
        yingyongbao {}
        //魅族
        meizu {}
        vivo {}
        //华为
        huawei {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [TD_CHANNEL_ID: flavor.name]
    }

    applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def flavorName = variant.productFlavors.get(0).name;
                        def developV = variant.buildType.name;
                        def fileName = "zjb_v${defaultConfig.versionName}_${flavorName}_${releaseTime()}_${developV}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
            }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
    compile 'com.android.support:recyclerview-v7:24.1.1'
    compile project(':extras:zjb-image-loader')
    compile project(':extras:zjb-sync-volley')
    compile project(':extras:zjb-mp3recorder')
    compile project(':extras:zjb-js-bridge')
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.jakewharton:butterknife:5.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.joooonho:selectableroundedimageview:1.0.1'
    compile 'com.commit451:PhotoView:1.2.5'
    // 年份选择器aar canvas画的，该库20k大小
    compile(name: 'wheellib-release', ext: 'aar')
    // 七牛上传
    compile 'com.qiniu:qiniu-android-sdk:7.1.2'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    compile 'com.soundcloud.android:android-crop:1.0.1@aar'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
}
